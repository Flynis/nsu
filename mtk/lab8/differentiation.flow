import expression;
import rational;

export {
    differentiate(e: Expr, variable: string) -> Expr;
}

simplifyAdd(l: Expr, r: Expr) -> Expr {
    lValue: RationalNum = calculateNoVars(l);
    rValue: RationalNum = calculateNoVars(r);
    if (!isNan(lValue) && !isNan(rValue)) {
        Rational(rationalAdd(lValue, rValue));
    } else if (isZero(lValue)) {
        r;
    } else if (isZero(rValue)) {
        l;
    } else if (l == Neg(r) || r == Neg(l)) {
        Int(0);
    } else {
        addExpressions(l, r);
    }
}

simplifySub(l: Expr, r: Expr) -> Expr {
    lValue: RationalNum = calculateNoVars(l);
    rValue: RationalNum = calculateNoVars(r);
    if (!isNan(lValue) && !isNan(rValue)) {
        Rational(rationalSub(lValue, rValue));
    } else if (isZero(lValue)) {
        Neg(r);
    } else if (isZero(rValue)) {
        l;
    } else if (l == r) {
        Int(0);
    } else {
        subExpressions(l, r);
    }
}

simplifyMul(l: Expr, r: Expr) -> Expr {
    lValue: RationalNum = calculateNoVars(l);
    rValue: RationalNum = calculateNoVars(r);
    if (!isNan(lValue) && !isNan(rValue)) {
        Rational(rationalMul(lValue, rValue));
    } else if (isZero(lValue) || isZero(rValue)) {
        Int(0);
    } else if (isOne(rValue)) {
        l;
    } else if (isOne(lValue)) {
        r;
    } else {
        mulExpressions(l, r);
    }
}

simplifyDiv(l: Expr, r: Expr) -> Expr {
    lValue: RationalNum = calculateNoVars(l);
    rValue: RationalNum = calculateNoVars(r);
    if (!isNan(lValue) && !isNan(rValue)) {
        Rational(rationalDiv(lValue, rValue));
    } else if (isOne(rValue)) {
        l;
    } else if (isZero(lValue)) {
        Int(0);
    } else {
        divExpressions(l, r);
    }
}

simplifyBinary(l: Expr, r: Expr, op: BinaryOp) -> Expr {
    switch(op) {
        AddOp(): simplifyAdd(l, r);
        SubOp(): simplifySub(l, r);
        MulOp(): simplifyMul(l, r);
        DivOp(): simplifyDiv(l, r);
    };
}

simplifyNeg(e: Expr) -> Expr {
    value: RationalNum = calculateNoVars(e);
    if (value != nan()) {
        Rational(rationalMul(i2r(-1), value));
    } else {
        Neg(e);
    }
}

simplify(e: Expr) -> Expr {
    switch(e) {
        Neg(expr): simplifyNeg(expr);
        Binary(l, r, op): {
            ls: Expr = simplify(l);
            rs: Expr = simplify(r);
            simplifyBinary(ls, rs, op);
        }
        default: e;
    }
}

differentiate(e: Expr, variable: string) -> Expr {
    switch(e) {
        Var(name): if(name == variable) Int(1) else Int(0);
        Neg(expr): simplifyNeg(differentiate(expr, variable));
        Binary(l, r, op): {
            ld: Expr = differentiate(l, variable);
            rd: Expr = differentiate(r, variable);
            switch(op) {
                AddOp(): simplifyAdd(ld, rd);
                SubOp(): simplifySub(ld, rd);
                MulOp(): simplifyAdd(simplifyMul(l, rd), simplifyMul(ld, r));
                DivOp(): simplifyDiv(simplifySub(simplifyMul(ld, r), simplifyMul(l, rd)), simplifyMul(r, r));
            };
        }
        default: Int(0);
    }
}

